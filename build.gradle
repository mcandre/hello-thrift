plugins {
  id 'com.github.johnrengelman.shadow' version '1.2.2'
  id 'org.jruyi.thrift' version '0.3.1'
}

apply plugin: 'java'
apply plugin: 'org.jruyi.thrift'
apply plugin: 'checkstyle'
apply plugin: 'pmd'
apply plugin: 'jacoco'

sourceCompatibility = '1.6'

compileJava {
  options.compilerArgs << '-Xlint:all'
}

checkstyle {
  configProperties = [configDir: "${rootDir}/config"]
}

pmd { consoleOutput = true }
pmdMain { exclude '**/thrift/**' }

jacocoTestReport {
  afterEvaluate {
    classDirectories = files(
      classDirectories.files.collect {
        fileTree(dir: it, exclude: '**/thrift/**')
      }
    )
  }
}

jar {
  manifest {
    attributes 'Main-Class': 'us.yellosoft.hello.Hello'
  }
}

repositories {
  mavenCentral()
}

dependencies {
  compile 'org.apache.thrift:libthrift:0.9.0'

  testCompile 'junit:junit:4.12'
  testCompile 'org.slf4j:slf4j-log4j12:1.7.12'
}
